apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
//        applicationId "com.github.jinsedeyuzhou.ijkplayer"
        minSdkVersion 11
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.1.0'
    compile 'tv.danmaku.ijk.media:ijkplayer-java:0.6.2'
    compile 'tv.danmaku.ijk.media:ijkplayer-armv7a:0.6.2'
    // # Other ABIs: optional
    compile 'tv.danmaku.ijk.media:ijkplayer-armv5:0.6.2'
    compile 'tv.danmaku.ijk.media:ijkplayer-arm64:0.6.2'
    compile 'tv.danmaku.ijk.media:ijkplayer-x86:0.6.2'
    compile 'tv.danmaku.ijk.media:ijkplayer-x86_64:0.6.2'
    //# ExoPlayer as IMediaPlayer: optional, experimental
    compile 'tv.danmaku.ijk.media:ijkplayer-exo:0.6.2'

    compile 'com.github.ctiao:DanmakuFlameMaster:0.7.1'
    compile 'com.github.ctiao:ndkbitmap-armv7a:0.7.1'

    //# Other ABIs: optional
//    compile 'com.github.ctiao:ndkbitmap-armv5:0.7.1'
//    compile 'com.github.ctiao:ndkbitmap-x86:0.7.1'
}

version = "1.0.1"

// 定义两个链接，下面会用到。
def siteUrl = 'https://github.com/jinsedeyuzhou/VideoPlayer' // 项目主页。
def gitUrl = 'https://github.com/jinsedeyuzhou/VideoPlayer.git' // Git仓库的url。

group = "com.wangyuxuan"
install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar'
                // 项目描述，复制我的话，这里需要修改。
                name 'Player For Android'// 可选，项目名称。
                description 'VideoPlayer is an media player for Android base on ijkplayer..'// 可选，项目描述。
                url siteUrl // 项目主页，这里是引用上面定义好。

                // 软件开源协议，现在一般都是Apache License2.0吧，复制我的，这里不需要修改。
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                //填写开发者基本信息，复制我的，这里需要修改。
                developers {
                    developer {
                        id 'wangyuxuan' // 开发者的id。
                        name 'wangyuxuan' // 开发者名字。
                        email 'jinsedeyuzhou@gmail.com' // 开发者邮箱。
                    }
                }

                // SCM，复制我的，这里不需要修改。
                scm {
                    connection gitUrl // Git仓库地址。
                    developerConnection gitUrl // Git仓库地址。
                    url siteUrl // 项目主页。
                }
            }
        }
    }
}
// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // destinationDir = file("../javadoc/")
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}
// 生成javaDoc的jar，不需要修改。
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user") // Bintray的用户名。
    key = properties.getProperty("bintray.apikey") // Bintray刚才保存的ApiKey。

    configurations = ['archives']
    pkg {
        repo = "maven"  // 上传到maven库。（这里要特别注意，如果写了maven报404错误，请在bintray创建一个仓库，这里填改成你创建的仓库的名字，如何创建请看下图。）
        name = "VideoPlayer"  // 发布到Bintray上的项目名字，这里的名字不是compile 'com.yanzhenjie:andserver:1.0.1'中的andserver。
        userOrg = 'jinsedeyuzhou' // Bintray的用户名，2016年11月更新。
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true // 是否是公开项目。
    }
}

//com.com.wangyuxuan:videoplayer:1.0.1